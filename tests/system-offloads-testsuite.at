AT_INIT

AT_COPYRIGHT([Copyright (c) 2016 Mellanox Technologies, Ltd.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at:

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.])

m4_ifdef([AT_COLOR_TESTS], [AT_COLOR_TESTS])

m4_include([tests/ovs-macros.at])
m4_include([tests/ovsdb-macros.at])
m4_include([tests/ofproto-macros.at])
m4_include([tests/system-common-macros.at])
m4_include([tests/system-kmod-macros.at])

m4_include([tests/system-offloads-traffic.at])

# The goal is to run as many as possible of the system-traffic tests with
# OVS tc offload enabled. We do this by overriding the
# OVS_TRAFFIC_VSWITCHD_START() with offloading enabled.
m4_define([OVS_TRAFFIC_VSWITCHD_START],
  [AT_CHECK([modprobe openvswitch])
   on_exit 'modprobe -r openvswitch'
   m4_foreach([mod], [[vport_geneve], [vport_gre], [vport_lisp], [vport_stt], [vport_vxlan]],
              [modprobe -q mod || echo "Module mod not loaded."
               on_exit 'modprobe -q -r mod'
              ])
   on_exit 'ovs-dpctl del-dp ovs-system'
   on_exit 'ovs-appctl dpctl/flush-conntrack'
   _OVS_VSWITCHD_START([], [-- set Open_vSwitch . other_config:hw-offload=true
   $3])
   dnl Add bridges, ports, etc.
   AT_CHECK([ovs-vsctl -- _ADD_BR([br0]) -- $1 m4_if([$2], [], [], [| uuidfilt])], [0], [$2])
])


# We override the OVS_REVALIDATOR_PURGE macro, allowing a bit more time for the
# tc-datapath entries to be installed.
m4_define([OVS_REVALIDATOR_PURGE],
    [AT_CHECK([sleep 2; ovs-appctl revalidator/purge], [0])])


# We override the DPCTL_DUMP_CONNTRACK macro, allowing a bit more time for the
# tc-datapath conntrack entries to be installed/updated.
m4_define([DPCTL_DUMP_CONNTRACK], [sleep 3; ovs-appctl dpctl/dump-conntrack])


# Conntrack ALGs are not supported for tc.
m4_define([CHECK_CONNTRACK_ALG],
[
     AT_SKIP_IF([:])
])


# Conntrack timeout not supported for tc.
m4_define([CHECK_CONNTRACK_TIMEOUT],
[
     AT_SKIP_IF([:])
])


# The list below are tests that will not pass for a "test environment" specific
# issue.
m4_define([OVS_TEST_SKIP_LIST],
    [ovs_test_skip_list="
datapath - truncate and output to gre tunnel by simulated packets
datapath - truncate and output to gre tunnel
conntrack - multiple namespaces, internal ports
conntrack - ct metadata, multiple zones
conntrack - ICMP related
conntrack - ICMP related to original direction
conntrack - IPv4 fragmentation + cvlan
conntrack - IPv4 fragmentation with fragments specified
conntrack - IPv6 fragmentation + cvlan
conntrack - Fragmentation over vxlan
conntrack - IPv6 Fragmentation over vxlan
conntrack - multiple zones, local
conntrack - multi-stage pipeline, local
conntrack - ICMP related with NAT
conntrack - DNAT load balancing
conntrack - DNAT load balancing with NC
conntrack - Multiple ICMP traverse
conntrack - can match and clear ct_state from outside OVS
IGMP - flood under normal action"
echo "$ovs_test_skip_list" | sed "s/<SPC>/ /g"])

m4_include([tests/system-traffic.at])
